import org.apache.batik.transcoder.TranscoderInput
import org.apache.batik.transcoder.TranscoderOutput
import org.apache.batik.transcoder.image.PNGTranscoder
import org.apache.batik.transcoder.SVGAbstractTranscoder

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'xml-apis:xml-apis:1.3.04'
        classpath 'xml-apis:xml-apis-ext:1.3.04'
        classpath 'batik:batik-rasterizer:1.6'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'org.jetbrains.intellij' version '0.2.13'
    id 'net.ltgt.apt' version '0.10'
}

def isCi = System.env.CI != null

group 'mr.intellij.plugin.autofactory'
version '0.2.4'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def intellijVersions = [
        'oldest': 'IC-2016.3',
        'latest': 'IC-2017.2.1',
        'eap'   : 'LATEST-EAP-SNAPSHOT'
]

intellij {
    def intellijVersion = intellijVersions[System.env.INTELLIJ_VERSION ?: 'latest']
    println "Picked IntelliJ $intellijVersion"
    version intellijVersion
    pluginName 'Google AutoFactory Support'
    downloadSources !isCi
    updateSinceUntilBuild false
}

dependencies {
    def lombok = 'org.projectlombok:lombok:1.16.16'
    compileOnly lombok
    testCompileOnly lombok
    apt lombok

    compile 'com.google.auto.factory:auto-factory:1.0-beta5'
    compile 'com.google.guava:guava:22.0'
    compile 'com.google.inject:guice:4.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'org.powermock:powermock-core:1.7.0'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.0'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

task generateGutterIcons {
    def transcoder = new PNGTranscoder()
    transcoder.addTranscodingHint(SVGAbstractTranscoder.KEY_HEIGHT, 16.0F)

    file('./artwork/gutter-icons').listFiles().each { f ->
        def filename = f.name.take(f.name.lastIndexOf('.'))
        file("src/main/resources/gutter/icons/${filename}.png").newOutputStream().withCloseable { os ->
            def input = new TranscoderInput(f.toURI().toURL().toString())
            def output = new TranscoderOutput(os)
            transcoder.transcode(input, output)
        }
    }
}
